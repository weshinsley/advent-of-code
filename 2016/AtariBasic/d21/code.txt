0 ? CHR$(125);"AOC 2016, Day 21 .. LOAD..":DIM C$(40),S$(8),A$(1),SC$(300):I=1:J=1:OPEN #1,4,0,"H:INPUT.TXT":TRAP 5
1 GET #1,A:IF A<>10 THEN C$(I,I)=CHR$(A):I=I+1:GOTO 1
2 RESTORE :FOR X=1 TO 7:READ L,S$,A,B:IF C$(1,L)=S$ THEN POP :GOTO 4
3 NEXT X
4 SC$(J,J)=CHR$(48+X):SC$(J+1,J+1)=C$(A):SC$(J+2,J+2)=C$(B):J=J+3:I=1:GOTO 1
5 DIM U$(8):DATA 6,swap p,15,31,6,swap l,13,27,8,rotate l,13,13,8,rotate r,14,14,8,rotate b,36,36,1,m,15,29,3,rev,19,29
6 DIM B$(1),T$(8):T$="abcdefgh":FOR J=1 TO 300 STEP 3:? SC$(J,J+2);" ";T$;:ON ASC(SC$(J))-48 GOSUB 10,11,13,15,17,19,25
7 ? " -> ";T$
9 NEXT J:? T$:? "PRESS A KEY...":CLOSE #1:OPEN #1,4,0,"K:":GET #1,J:CLOSE #1:GOTO 26
10 X=ASC(SC$(J+1))-47:Y=ASC(SC$(J+2))-47:A$=T$(X,X):T$(X,X)=T$(Y,Y):T$(Y,Y)=A$:RETURN :REM SWAP_P(X,Y)
11 X=0:Y=0:A$=SC$(J+1):B$=SC$(J+2):FOR Z=1 TO 8:X=X+Z*(T$(Z,Z)=A$):Y=Y+Z*(T$(Z,Z)=B$):NEXT Z
12 A$=T$(X,X):T$(X,X)=T$(Y,Y):T$(Y,Y)=A$:RETURN :REM SWAP_L(X,Y)
13 X=ASC(SC$(J+1))-48:IF X>0 THEN FOR Y=1 TO X:A$=T$(1):T$(1,7)=T$(2,8):T$(8,8)=A$:NEXT Y
14 RETURN 
15 X=ASC(SC$(J+1))-48:IF X>0 THEN FOR Y=1 TO X:A$=T$(8):FOR Z=8 TO 2 STEP -1:T$(Z,Z)=T$(Z-1,Z-1):NEXT Z:T$(1,1)=A$:NEXT Y
16 RETURN 
17 A$=SC$(J+1):X=0:FOR Z=1 TO 8:X=X+Z*(T$(Z,Z)=A$):NEXT Z:X=X-1:X=1+X+(X>=4):FOR Y=1 TO X:A$=T$(8)
18 FOR Z=8 TO 2 STEP -1:T$(Z,Z)=T$(Z-1,Z-1):NEXT Z:T$(1,1)=A$:NEXT Y:RETURN 
19 X=ASC(SC$(J+1))-47:Y=ASC(SC$(J+2))-47:A$=T$(X,X):IF X=1 THEN T$=T$(2):GOTO 22
20 IF X=8 THEN T$=T$(1,7):GOTO 22
21 T$(X)=T$(X+1)
22 ON Y=1 GOTO 23:ON Y=8 GOTO 24:FOR X=8 TO Y STEP -1:T$(X,X)=T$(X-1,X-1):NEXT X:GOTO 24
23 FOR X=8 TO 2 STEP -1:T$(X,X)=T$(X-1,X-1):NEXT X
24 T$(Y,Y)=A$:RETURN 
25 X=ASC(SC$(J+1,J+1))-47:Y=ASC(SC$(J+2,J+2))-47:U$=T$:FOR Z=X TO Y:U$(Z,Z)=T$(Y-(Z-X),Y-(Z-X)):NEXT Z:T$=U$:RETURN 
26 T$="fbgdceah":DIM V$(8):FOR J=298 TO 1 STEP -3:? SC$(J,J+2);" ";T$;:ON ASC(SC$(J))-48 GOSUB 10,11,15,13,30,28,25
27 ? " -> ";T$:NEXT J:? T$:END 
28 X=ASC(SC$(J+2))-47:Y=ASC(SC$(J+1))-47:A$=T$(X,X):IF X=1 THEN T$=T$(2):GOTO 22
29 GOTO 20
30 A$=SC$(J+1):U$=T$
31 B$=U$(1,1):U$=U$(2,8):U$(8,8)=B$
32 V$=U$:X=0:FOR Z=1 TO 8:X=X+Z*(V$(Z,Z)=A$):NEXT Z:X=X-1:X=1+X+(X>=4):FOR Y=1 TO X:B$=V$(8)
33 FOR Z=8 TO 2 STEP -1:V$(Z,Z)=V$(Z-1,Z-1):NEXT Z:V$(1,1)=B$:NEXT Y
34 IF (V$<>T$) THEN GOTO 31
35 T$=U$:RETURN 
