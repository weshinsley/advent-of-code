0 GRAPHICS 18:? #6;"ADVENT OF CODE 2016":? #6;"DAY 9.":? #6;"loading...":OPEN #1,4,0,"H:INPUT.TXT":L=16384
1 GET #1,A:IF A<>10 THEN POKE L,A:L=L+1:POKE 709,A:GOTO 1
2 CLOSE #1:L=L-16384:POSITION 0,2:? #6;"loaded ";L;" chars":OB=ASC("("):POSITION 0,5:? #6;"PART 1 OR 2?"
3 POKE 709,10:OPEN #1,4,0,"K:":GET #1,P:CLOSE #1:ON P<>49 AND P<>50 GOTO 3:P=P-48:DIM A$(12),B$(12),C$(12):BRACKET=30
4 FOR I=1 TO 12:A$(I,I)=CHR$(0):NEXT I:A$(12,12)="0":ADD=20:POSITION 0,5:? #6;"             ":Q=0:ON P=2 GOTO 9
5 POSITION 0,5:? #6;A$:ON Q>=L GOTO 7:IF PEEK(16384+Q)<>OB THEN B$="1":GOSUB ADD:Q=Q+1:GOTO 5
6 GOSUB BRACKET:B$=STR$(C*F):Q=Q+C:GOSUB ADD:GOTO 5
7 POKE 764,255:POSITION 0,7:? #6;"done - PRESS ANY KEY"
8 POKE 77,0:POKE 709,PEEK(20):ON PEEK(764)=255 GOTO 8:RUN 
9 DEPTH=1:DIM X1(10),X2(10),M(10),RD$(120):RD$(1)=CHR$(0):RD$(120)=CHR$(0):RD$(2)=RD$:RD$(12,12)="0"
10 X1(1)=0:X2(1)=L
11 RECURSE=12:REM FUNCTION(DEPTH)
12 A$=RD$(1+((DEPTH-1)*12),DEPTH*12):Q=X1(DEPTH)
13 ON Q=X2(DEPTH) GOTO 19:IF PEEK(16384+Q)<>OB THEN B$="1":GOSUB ADD:RD$(1+((DEPTH-1)*12),DEPTH*12)=A$:Q=Q+1:GOTO 13
14 GOSUB BRACKET:X1(DEPTH)=Q+C:M(DEPTH)=F
15 DEPTH=DEPTH+1:FOR I=1 TO 12:B$(I,I)=CHR$(0):NEXT I:B$(12,12)="0":RD$(1+((DEPTH-1)*12),DEPTH*12)=B$
16 X2(DEPTH)=X1(DEPTH-1):X1(DEPTH)=X2(DEPTH)-C:GOSUB RECURSE
17 B$=RD$(1+((DEPTH-1)*12),DEPTH*12):DEPTH=DEPTH-1:A$=RD$(1+((DEPTH-1)*12),DEPTH*12)
18 FOR J=1 TO M(DEPTH):GOSUB ADD:NEXT J:RD$(1+((DEPTH-1)*12),DEPTH*12)=A$:GOTO 12
19 POSITION 0,5:? #6;RD$(1,12):ON DEPTH=1 GOTO 7:POKE 77,0:RETURN 
20 REM A$ = A$ + B$ FOR BIG INTEGERS...
21 FOR I=1 TO 12:C$(I,I)=CHR$(0):NEXT I:C$(13-LEN(B$),12)=B$:B$=C$:CARRY=0
22 FOR I=12 TO 1 STEP -1:IF A$(I,I)=CHR$(0) AND B$(I,I)=CHR$(0) AND CARRY=0 THEN POP :GOTO 27
23 IF A$(I,I)=CHR$(0) THEN A$(I,I)="0"
24 IF B$(I,I)=CHR$(0) THEN B$(I,I)="0"
25 V=(ASC(A$(I,I))-48)+(ASC(B$(I,I))-48)+CARRY:CARRY=0:IF V>=10 THEN V=V-10:CARRY=1
26 A$(I,I)=CHR$(V+48):NEXT I
27 RETURN 
30 REM PARSE (CXF) INTO C AND F. ASSUME Q STARTS AT (
31 Q=Q+1:C=PEEK(16384+Q)-48
32 Q=Q+1:V=PEEK(16384+Q):IF V<58 THEN C=C*10+(V-48):GOTO 32
33 Q=Q+1:F=PEEK(16384+Q)-48
34 Q=Q+1:V=PEEK(16384+Q):IF V>47 THEN F=F*10+(V-48):GOTO 34
35 Q=Q+1:RETURN 
