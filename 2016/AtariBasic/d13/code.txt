0 GRAPHICS 15:? "Advent of code 2016 - day 13":OPEN #1,4,0,"H:INPUT.TXT":V=0:GET #1,A:COLOR 1
1 V=V*10+(A-48):GET #1,A:ON A<>10 GOTO 1:CLOSE #1:XM=50:YM=50
2 FOR X=0 TO XM:FOR Y=0 TO YM:Z=(X+Y):Z=(Z*Z)+X+X+X+Y+V:B=0
3 IF Z>0 THEN ZZ=INT(Z/2)*2:B=B+(ZZ<>Z):Z=INT(Z/2):GOTO 3
4 IF (INT(B/2)<>B/2) THEN PLOT X,Y
5 NEXT Y:NEXT X
6 COLOR 2:PLOT 1,1
7 X=1:Y=1:D=0:DIM S$(1000):MD=9999:XT=31:YT=39
8 POKE 77,0:IF XT=X AND YT=Y THEN MD=D
9 IF D>=MD THEN GOTO 15
10 IF (Y<YM) THEN LOCATE X,Y+1,Z:IF Z=0 THEN D=D+1:S$(D,D)="S":COLOR 2:Y=Y+1:PLOT X,Y:GOTO 8
11 IF (X<XM) THEN LOCATE X+1,Y,Z:IF Z=0 THEN D=D+1:S$(D,D)="E":COLOR 2:X=X+1:PLOT X,Y:GOTO 8
12 IF (Y>0) THEN LOCATE X,Y-1,Z:IF Z=0 THEN D=D+1:S$(D,D)="N":COLOR 2:Y=Y-1:PLOT X,Y:GOTO 8
13 IF (X>0) THEN LOCATE X-1,Y,Z:IF Z=0 THEN D=D+1:S$(D,D)="W":COLOR 2:X=X-1:PLOT X,Y:GOTO 8
15 IF D=0 THEN ? MD;" ... ";:GOTO 20
16 COLOR 0:PLOT X,Y:IF S$(D,D)="S" THEN Y=Y-1:D=D-1:GOTO 11
17 IF S$(D,D)="E" THEN X=X-1:D=D-1:GOTO 12
18 IF S$(D,D)="N" THEN Y=Y+1:D=D-1:GOTO 13
19 IF S$(D,D)="W" THEN X=X+1:D=D-1:GOTO 15
20 C=1:XM1=XM+1:YM1=YM+1:DIM S2$(XM1*YM1):COLOR 3:FOR X=0 TO XM:FOR Y=0 TO YM:I=(Y*YM1)+X+1
21 LOCATE X,Y,Z:IF Z=1 THEN S2$(I,I)=CHR$(255):PLOT X,Y:GOTO 23
22 S2$(I,I)=CHR$(254)
23 NEXT Y:NEXT X:COLOR 2:X=1:Y=1:PLOT X,Y:S2$(XM+3,XM+3)=CHR$(0):D=2:S$(1,1)=CHR$(X):S$(2,2)=CHR$(Y)
24 IF D=0 THEN GOTO 37
25 X=ASC(S$(1,1)):Y=ASC(S$(2,2)):D=D-2:I=(Y*XM1)+X+1:Z=ASC(S2$(I,I)):IF LEN(S$)=2 THEN S$="":GOTO 27
26 S$=S$(3)
27 IF Z=50 THEN GOTO 24
28 ON Y=0 GOTO 30:I=((Y-1)*XM1)+X+1:Q=ASC(S2$(I,I)):IF Q=255 OR (Q<254 AND Q<=Z+1) THEN 30
29 C=C+(Q=254):D=D+1:S$(D,D)=CHR$(X):D=D+1:S$(D,D)=CHR$(Y-1):S2$(I,I)=CHR$(Z+1):PLOT X,Y-1
30 ON Y=YM GOTO 32:I=((Y+1)*XM1)+X+1:Q=ASC(S2$(I,I)):IF Q=255 OR (Q<254 AND Q<=Z+1) THEN 32
31 C=C+(Q=254):D=D+1:S$(D,D)=CHR$(X):D=D+1:S$(D,D)=CHR$(Y+1):S2$(I,I)=CHR$(Z+1):PLOT X,Y+1
32 ON X=0 GOTO 34:I=(Y*XM1)+X:Q=ASC(S2$(I,I)):IF Q=255 OR (Q<254 AND Q<=Z+1) THEN 34
33 C=C+(Q=254):D=D+1:S$(D,D)=CHR$(X-1):D=D+1:S$(D,D)=CHR$(Y):S2$(I,I)=CHR$(Z+1):PLOT X-1,Y
34 ON X=XM GOTO 36:I=(Y*XM1)+X+2:Q=ASC(S2$(I,I)):IF Q=255 OR (Q<254 AND Q<=Z+1) THEN 36
35 C=C+(Q=254):D=D+1:S$(D,D)=CHR$(X+1):D=D+1:S$(D,D)=CHR$(Y):S2$(I,I)=CHR$(Z+1):PLOT X+1,Y
36 GOTO 24
37 ? C
