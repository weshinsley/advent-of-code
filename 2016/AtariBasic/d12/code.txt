0 REM AOC 2016, DAY 12, ATARI BASIC.
1 OPEN #1,4,0,"H:INPUT.TXT":POKE 82,0:GRAPHICS 0:TRAP 14:POSITION 0,6:? "POKE 842,12:CONT":L=101:DIM S$(3)
2 POSITION 0,3:? L;" ";
3 FOR I=1 TO 3:GET #1,A:S$(I,I)=CHR$(A):NEXT I:I=0:GET #1,A
4 IF S$="inc" THEN GET #1,A:A=A-32:? CHR$(A);"=";CHR$(A);"+1":GET #1,A:GOTO 12
5 IF S$="dec" THEN GET #1,A:A=A-32:? CHR$(A);"=";CHR$(A);"-1":GET #1,A:GOTO 12
6 IF S$<>"jnz" THEN GOTO 9
7 GET #1,A:A=A-32*(A>65):? "IF ";CHR$(A);"<>0 THEN ";:GET #1,A:GET #1,A:S=1:IF A=45 THEN S=-1:GET #1,A
8 ? L+(S*(A-48)):GET #1,A:GOTO 12
9 S$="":I=1
10 GET #1,A:IF A<>32 THEN A=A-32*(A>65):S$(I,I)=CHR$(A):I=I+1:GOTO 10
11 GET #1,A:? CHR$(A-32);"=";S$:GET #1,A
12 POSITION 0,0:POKE 842,13:STOP 
13 POSITION 0,3:? "                         ";:L=L+1:GOTO 2
14 ? CHR$(125);"Running...."
100 A=0:B=0:C=0:D=0:TRAP 999
998 GOTO 32767
999 ? A:A=0:B=0:C=1:D=0:TRAP 1000:GOTO 101
1000 ? A
