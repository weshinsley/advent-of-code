0 ? CHR$(125);"Advent of Code 2016, Day 4":? "in Atari Basic":? :? "a:";:DIM C(26),M(5):OPEN #1,4,0,"H:INPUT.TXT":R=0
1 DIM S$(80):? :? :? :? :? 
2 MX=0:TRAP 11:FOR I=1 TO 26:C(I)=0:NEXT I:I=1:N=0:S$(1)=" ":S$(80)=" ":S$(2)=S$:L=1
3 GET #1,A:IF A>96 AND A<123 THEN S$(L,L)=CHR$(A):L=L+1:A=A-96:C(A)=C(A)+1:MX=(MX*(C(A)<=MX))+(C(A)*(C(A)>MX)):GOTO 3
4 IF A=45 THEN S$(L,L)=CHR$(A):L=L+1:GOTO 3
5 IF A>47 AND A<58 THEN N=(N*10)+(A-48):GET #1,A:GOTO 5
6 FOR J=1 TO 26:IF C(J)=MX THEN M(I)=J:C(J)=-1:I=I+1:IF I=6 THEN POP :GOTO 8
7 NEXT J:IF I<6 THEN MX=MX-1:GOTO 6
8 FOR J=1 TO 5:GET #1,A:IF A-96<>M(J) THEN POP :GOTO 10
9 NEXT J:R=R+N:GOSUB 20
10 GET #1,A:ON A<>10 GOTO 10:GOTO 2
11 CLOSE #1:POSITION 5,3:? R:POSITION 5,18:END 
20 FOR I=1 TO L-1:IF S$(I,I)="-" THEN S$(I,I)=" ":GOTO 24
21 V=(ASC(S$(I,I))-97)+N
22 IF (V>=26) THEN V=V-26:GOTO 22
23 S$(I,I)=CHR$(V+97)
24 NEXT I
25 FOR I=1 TO L-5:IF S$(I,I)<>"n" THEN 27
26 IF S$(I+1,I+1)="o" AND S$(I+2,I+2)="r" THEN ? S$;" AT ";N:POP :GOTO 28
27 NEXT I
28 RETURN 
